is_mac()     { [[ $OSTYPE == darwin*   ]] }
is_linux()   { [[ $OSTYPE == linux-gnu ]] }
has()        { [[ -n "${commands[$1]}" ]] }

# begin PROMPT

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git:*' formats '%b%u%c '
zstyle ':vcs_info:git:*' actionformats '%a %m %b '
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr " %F{green}+"
zstyle ':vcs_info:*' unstagedstr " %F{red}*"

setopt prompt_subst

# orange lambda
__PROMPT_SYMBOL='%F{166}Î»%f'
__PROMPT_TIME='%F{white}%D{%H:%M}%f'
__PROMPT_RETURN_CODE='%(?..%F{red}?%?%f )'
__PROMPT_DIR='%F{green}%~%f'

PROMPT=$__PROMPT_DIR' $(__prompt_git_status)'"$__PROMPT_SYMBOL "
RPROMPT='$(__prompt_exec_time)'$__PROMPT_RETURN_CODE$__PROMPT_TIME

function preexec() {
    timer=$SECONDS
}

function precmd() {
    unset timer
    vcs_info
}

function __prompt_exec_time() {
    local timer_seconds=$(($SECONDS - ${timer:-$SECONDS}))
    if [ $timer_seconds -gt 0 ]; then
        echo -n '%F{cyan}~> '
        local timer_minutes=$(($timer_seconds / 60))
        local timer_seconds=$(($timer_seconds % 60))
        if [ $timer_minutes -gt 0 ]; then
            echo -n "${timer_minutes}m"
        fi
        echo -n "${timer_seconds}s%f "
    fi
}

function __prompt_git_status() {
    echo -n "%B%F{blue}${vcs_info_msg_0_}%f%b"
}
# end PROMPT

#begin znap
if [[ ! -r "$HOME/.local/share/zsh/plugins/znap/znap.zsh" ]]; then
    git clone --depth 1 https://github.com/marlonrichert/zsh-snap.git ~/.local/share/zsh/plugins/znap/
fi

source ~/.local/share/zsh/plugins/znap/znap.zsh

znap prompt

function znap_source() {
    znap clone $1
    znap source $(echo $1 | cut  -d '/' -f 2)
}

znap_source 'git@github.com:jeffreytse/zsh-vi-mode'
znap_source 'git@github.com:zsh-users/zsh-autosuggestions'
znap_source 'git@github.com:zdharma-continuum/fast-syntax-highlighting'
znap_source 'git@github.com:MichaelAquilina/zsh-you-should-use'
znap_source 'git@github.com:MichaelAquilina/zsh-auto-notify'
znap_source 'git@github.com:zsh-users/zsh-completions'

znap clone 'git@github.com:ohmyzsh/ohmyzsh'
znap source ohmyzsh lib/completion
znap source ohmyzsh lib/history
znap source ohmyzsh lib/git
znap source ohmyzsh lib/directories # ls and cd aliases
znap source ohmyzsh plugins/git
znap source ohmyzsh plugins/golang

znap clone 'git@github.com:trapd00r/LS_COLORS'
znap source LS_COLORS lscolors

ZSH_WEB_SEARCH_ENGINES=(
  cambridge "https://dictionary.cambridge.org/dictionary/english/"
  verbformen "https://www.verbformen.ru/?w="
)
znap source ohmyzsh plugins/web-search

fpath+=( ~[zsh-completions]/src )
znap fpath _docker 'docker completion zsh'

#end znap

export EDITOR=vim

set -o vi

export PATH=$HOME/.local/bin/:$PATH

if has go; then
    export PATH=$PATH:$(go env GOPATH)/bin/
fi

if has nvim; then
    alias vim=nvim
    export EDITOR=nvim
fi
alias vi=vim
alias v=vim

if has bat; then
    alias cat="bat --plain"
fi

alias rm="rm -i"
alias mv="mv -i"

if is_mac; then
    alias ls="gls --color"
fi

alias gld="git -c diff.external=difft log --ext-diff"
alias gshd="git -c diff.external=difft show --ext-diff"
alias gdd="git -c diff.external=difft diff"

# don't use git checkout
unalias gco gcb gcB gcd gcm gcor

alias mvprogress="rsync -aP --remove-source-files"

REAL_SSH=$(which ssh)

function ssh() {
    if [[ -n ${TMUX_PANE} ]]; then
        tmux rename-window -t ${TMUX_PANE} "ssh $@"
    fi
    $REAL_SSH "$@"
    if [[ -n ${TMUX_PANE} ]]; then
        tmux setw automatic-rename
    fi
}

function git-vimdiff () {
    GIT_EXTERNAL_DIFF=git-diff-wrapper git --no-pager diff $@;
}

if is_linux; then
    alias pbcopy='xclip -selection clipboard'
    alias pbpaste='xclip -selection clipboard -o'
    alias open='xdg-open'
fi

# begin FZF
export FZF_DEFAULT_OPTS="--cycle --color=gutter:-1 --pointer '>' --border rounded --highlight-line"
export FZF_CTRL_R_OPTS='--height=100% --margin 15%'

#
zvm_after_init_commands+=('[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh')

if [ -e ~/.zshrc.local ]; then
    source ~/.zshrc.local
fi

# end FZF

# never ever beep ever
setopt NO_BEEP

autoload -U compinit
compinit
