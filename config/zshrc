is_mac()     { [[ $OSTYPE == darwin*   ]] }
is_linux()   { [[ $OSTYPE == linux-gnu ]] }
has()        { [[ -n "${commands[$1]}" ]] }

#begin znap
if [[ ! -r "$HOME/.local/share/zsh/plugins/znap/znap.zsh" ]]; then
    git clone --depth 1 https://github.com/marlonrichert/zsh-snap.git ~/.local/share/zsh/plugins/znap/
fi

source ~/.local/share/zsh/plugins/znap/znap.zsh

fpath+=( ~/.config/zsh )
autoload -Uz promptinit
promptinit
prompt aufforderung
znap prompt

function znap_source() {
    znap clone $1
    znap source $(echo $1 | cut  -d '/' -f 2)
}

znap_source 'git@github.com:jeffreytse/zsh-vi-mode'
znap_source 'git@github.com:zsh-users/zsh-autosuggestions'
znap_source 'git@github.com:zdharma-continuum/fast-syntax-highlighting'

export AUTO_NOTIFY_THRESHOLD=30
export AUTO_NOTIFY_CANCEL_ON_SIGINT=1
export AUTO_NOTIFY_EXPIRE_TIME=5000
export AUTO_NOTIFY_URGENCY_ON_ERROR="normal"
znap_source 'git@github.com:molchalin/zsh-auto-notify'
znap_source 'git@github.com:zsh-users/zsh-completions'
znap_source 'git@github.com:romkatv/zsh-defer'
znap_source 'git@github.com:olets/zsh-abbr'

znap clone 'git@github.com:ohmyzsh/ohmyzsh'
znap source ohmyzsh lib/completion
znap source ohmyzsh lib/history
znap source ohmyzsh lib/directories # ls and cd aliases

ZSH_WEB_SEARCH_ENGINES=(
  cambridge "https://dictionary.cambridge.org/dictionary/english/"
  verbformen "https://www.verbformen.ru/?w="
)

znap source ohmyzsh lib/functions
znap source ohmyzsh plugins/web-search

fpath+=( ~[zsh-completions]/src )
if has docker; then
     znap fpath _docker 'docker completion zsh'
fi

#end znap

export EDITOR=vim

export PATH=$HOME/.local/bin:$PATH

if has go; then
    export PATH=$PATH:$(go env GOPATH)/bin
fi

function add_abbr() {
    alias "$@"
    abbr --session --quiet "$@"
    local al=$(echo "$1" | cut -d '=' -f 1)
}
if has nvim; then
    export EDITOR=nvim
    add_abbr v=nvim
fi

set -o vi

if has bat; then
    alias cat="bat --plain"
fi

if has rg; then
    alias fgrep="rg -F"
fi

alias rm="rm -i"
alias mv="mv -i"

if has eza; then
    alias tree="eza --tree"
    alias ls="eza"
else
    if is_mac; then
        if has gls; then
            alias ls="gls --color"
        fi
    else
        alias ls="ls --color"
    fi
fi

add_abbr o="open"

add_abbr g=git

add_abbr "gc"="git commit"
add_abbr "gca"="git commit --amend"
add_abbr "gco"="git checkout"
add_abbr "gl"="git pull"
add_abbr "gp"="git push"
add_abbr "gpf"="git push --force"
add_abbr "gst"="git status"
add_abbr "grb"="git rebase"
add_abbr "grbc"="git rebase --continue"
add_abbr "grba"="git rebase --abort"
add_abbr "gd"="git diff"
add_abbr "gdt"="git difftool"
add_abbr "gdty"="git difftool -y"
add_abbr "ga"="git add"
add_abbr "gsth"="git stash"
add_abbr "gsthp"="git stash pop"
add_abbr "glg"="git log"

add_abbr mvprogress="rsync -aP --remove-source-files"

REAL_SSH=$(which ssh)

function ssh() {
    if [[ -n ${TMUX_PANE} ]]; then
        tmux rename-window -t ${TMUX_PANE} "ssh $@"
        zsh-defer tmux setw automatic-rename
    fi
    $REAL_SSH "$@"
}

if is_linux; then
    alias pbcopy='xclip -selection clipboard'
    alias pbpaste='xclip -selection clipboard -o'
    alias open='xdg-open'
fi

# begin FZF
export FZF_DEFAULT_OPTS="--cycle --color=gutter:-1 --pointer '>' --border rounded --highlight-line"
export FZF_CTRL_R_OPTS='--height=100% --margin 15%'

if has fzf; then
     zvm_after_init_commands+=('znap eval fzf-zsh "fzf --zsh"')
fi

# shift tab backward cycle
bindkey '^[[Z' reverse-menu-complete

if [ -e ~/.zshrc.local ]; then
    source ~/.zshrc.local
fi

# end FZF

export DFT_DISPLAY=side-by-side-show-both

function command_not_found_handler() {
    local RED='\033[0;31m'
    local BOLD_RED='\033[1;31m'
    local NC='\033[0m'
    echo "${RED}command not found: ${BOLD_RED}$1${NC}"
    return 127
}

# traps the shell quit and switch to the last session if it's the last pane
# if last session is not available anymore, switch to default
function __trap_exit_tmux() {
    [[ $(tmux list-windows | wc -l) -eq 1 ]] || return
    [[ $(tmux list-panes | wc -l) -eq 1 ]] || return
    local current_session=$(tmux display-message -p '#S')
    local next=$(tmux ls | cut -d: -f1 | fgrep -xv "${current_session}" | head -n 1)
    if [[ -n "$next" ]]; then
        tmux switch-client -t "$next"
    fi
}

if [[ -n "$TMUX" ]]; then
    trap __trap_exit_tmux EXIT
fi

# never ever beep ever
setopt NO_BEEP

# add % if there is no a trailing new line
setopt PROMPT_SP
setopt PROMPT_CR

if is_mac; then
  export TMUX_OS_ICON=" "
else
  if [ $(grep "Arch" /etc/*-release | wc -l) -gt "0" ]; then
    export TMUX_OS_ICON=" "
  elif [ $(grep "Ubuntu" /etc/*-release | wc -l) -gt "0" ]; then
    export TMUX_OS_ICON=" "
  fi
fi
