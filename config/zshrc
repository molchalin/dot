export ZSH=~/.oh-my-zsh
export EDITOR=nvim

set -o vi

ZSH_THEME=""
plugins=(
  git
  docker
  docker-compose
  golang
  web-search

  zsh-vi-mode
  zsh-autosuggestions
  fast-syntax-highlighting
  you-should-use
  auto-notify
  colored-man-pages
)

ZSH_WEB_SEARCH_ENGINES=(
  cambridge "https://dictionary.cambridge.org/dictionary/english/"
  verbformen "https://www.verbformen.ru/?w="
)

fpath+=${ZSH_CUSTOM:-${ZSH:-~/.oh-my-zsh}/custom}/plugins/zsh-completions/src

source $ZSH/oh-my-zsh.sh

export PATH=$HOME/.local/bin/:$PATH

if go env GOPATH 2>&1 > /dev/null; then
    export PATH=$PATH:$(go env GOPATH)/bin/
fi

if which nvim 2>&1 > /dev/null; then
    alias vim=nvim
fi
alias vi=vim
alias v=vim

if which bat 2>&1 > /dev/null; then
    alias cat="bat --plain"
fi

alias rm="rm -i"

REAL_SSH=$(which ssh)

function ssh() {
    if [[ -n ${TMUX_PANE} ]]; then
        tmux rename-window -t ${TMUX_PANE} "ssh $@"
    fi
    $REAL_SSH "$@"
    if [[ -n ${TMUX_PANE} ]]; then
        tmux setw automatic-rename
    fi
}

function git-vimdiff () {
    GIT_EXTERNAL_DIFF=git-diff-wrapper git --no-pager diff $@;
}

if [[ "$(uname)" == 'Linux' ]]; then
    alias pbcopy='xclip -selection clipboard'
    alias pbpaste='xclip -selection clipboard -o'
    alias open='xdg-open'
fi

# begin PROMPT
# orange lambda
__PROMPT_SYMBOL='%F{166}λ%f'
__PROMPT_TIME='%F{white}%D{%H:%M}%f'
__PROMPT_RETURN_CODE='%(?..%F{red}?%?%f )'
__PROMPT_DIR='%F{green}%~%f'

PROMPT=$__PROMPT_DIR' $(__prompt_git_status)'"$__PROMPT_SYMBOL "
RPROMPT='$(__prompt_exec_time)'$__PROMPT_RETURN_CODE$__PROMPT_TIME

function preexec() {
    timer=$SECONDS
}

function __prompt_exec_time() {
    local timer_seconds=$(($SECONDS - ${timer:-$SECONDS}))
    if [ $timer_seconds -gt 0 ]; then
        echo -n '%F{cyan}~> '
        local timer_minutes=$(($timer_seconds / 60))
        local timer_seconds=$(($timer_seconds % 60))
        if [ $timer_minutes -gt 0 ]; then
            echo -n "${timer_minutes}m"
        fi
        echo -n "${timer_seconds}s%f "
    fi
}

function __prompt_git_status() {
    local message=""

    local unstaged=$(git status --porcelain 2>/dev/null | grep -e "^[MADRCU? ][MADRCU?]")

    local sign='%F{green}✔%f'
    if [[ -n $unstaged ]]; then
        sign='%F{red}✗%f'
    fi

    local branch=$(git rev-parse --abbrev-ref HEAD 2>/dev/null)
    if [[ -n ${branch} ]]; then
        message="%B%F{blue}(${branch} $sign%F{blue})%f%b "
    fi

    echo -n "${message}"
}
# end PROMPT

export FZF_DEFAULT_OPTS="--cycle --color=gutter:-1 --pointer '>' --border rounded --highlight-line"
export FZF_CTRL_R_OPTS='--height=100% --margin 15%'

zvm_after_init_commands+=('[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh')

if [ -e ~/.zshrc.local ]; then
    source ~/.zshrc.local
fi
